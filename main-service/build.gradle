plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.openapi.generator' version '6.6.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2022.0.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    // https://mvnrepository.com/artifact/org.springframework.data/spring-data-commons
    implementation 'org.springframework.data:spring-data-commons:3.1.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    // https://mvnrepository.com/artifact/org.openapitools/jackson-databind-nullable
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

openApiGenerate {
    inputSpec = "$project.rootDir/src/main/resources/templates/api-docs.yml".toString()
    outputDir = "$buildDir/generated/api" as Object
    generateModelDocumentation = true
    generatorName = "spring"
    library = "spring-boot"
    apiPackage = "com.example.userapi.controller"
    modelPackage = "com.example.userapi.model"
    invokerPackage = "com.example.userapi.api"
    configOptions = [
            useSpringBoot3: "true",
            useTags: "true",
            java8: "false",
            dateLibrary: "java8",
            interfaceOnly: "true",
            skipDefaultInterface: "true",
            hideGenerationTimestamp: "true",
            serializableModel: "true",
            booleanGetterPrefix: "is",
            openApiNullable: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/api/src/main/java")
        }
    }
}

tasks.compileJava {
    dependsOn(tasks.openApiGenerate)
}

tasks.named('test') {
    useJUnitPlatform()
}
